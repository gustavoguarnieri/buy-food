buildscript {
    ext {
        springBootVersion = "2.5.+"
        springfoxVersion = "3.0.0"
        keycloakVersion = "15.0.2"
        lombokVersion = "1.18.20"
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "jacoco"
    id "java"
    id "war"
    id "com.diffplug.spotless" version "5.14.2"
    id "org.sonarqube" version "3.3"
}

group = "br.com.example"
version = ""
sourceCompatibility = "11"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    implementation.exclude module: "spring-boot-starter-tomcat"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    providedRuntime "org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    implementation "io.springfox:springfox-swagger2:${springfoxVersion}"
    implementation "io.springfox:springfox-swagger-ui:${springfoxVersion}"
    implementation "io.springfox:springfox-boot-starter:${springfoxVersion}"
    implementation "org.modelmapper:modelmapper:2.4.4"
    implementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"
    implementation "org.keycloak:keycloak-admin-client:${keycloakVersion}"
    implementation "org.springframework.security:spring-security-core:5.5.1"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "io.projectreactor:reactor-test:3.4.9"
    runtimeOnly "mysql:mysql-connector-java:8.0.25"
    implementation "org.springframework.retry:spring-retry:1.3.1"
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        target fileTree(".") {
            include "**/*.java"
            exclude "**/build/**", "**/build-*/**"
        }
        toggleOffOn()
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target fileTree(".") {
            include "**/*.gradle"
            exclude "**/build/**", "**/build-*/**"
        }
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format "misc", {
        target fileTree(".") {
            include "**/*.md", "**/.gitignore"
            exclude "**/build/**", "**/build-*/**"
        }
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
}
